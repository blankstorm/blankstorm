precision highp float;

#include "utils.glslx"

// Attributes
attribute vec3 position;
attribute vec3 normal;

uniform mat4 world;
uniform mat4 worldViewProjection;
uniform mat4 rotation;
uniform vec3 camera;
uniform vec3 light;

uniform float base;
uniform float resolution;
uniform float seed;

uniform float groundAlbedo;
uniform vec3 halo;
uniform vec3 upperColor;
uniform vec3 lowerColor;
uniform vec2 lowerClamp;
uniform vec2 lowerClip;
uniform bool directNoise;
uniform vec2 range;

uniform bool clouds_enabled;
uniform float clouds_base;
uniform float clouds_albedo;

varying vec3 worldPosition;
varying vec3 _normal;

const vec3 cloud_magic = vec3(0.3, 0.59, 0.11);

export void vertexSource() {
	gl_Position = worldViewProjection * vec4(position, 1.0);
	worldPosition = vec3(world * vec4(position, 1.0));
	_normal = normal;

	// bump
	float depth = prand_at(normal, base, resolution, seed) - 0.5;
	vec3 bump = normal * depth;
	gl_Position = worldViewProjection * vec4(position + bump, 1.0);
}

export void fragmentSource() {
	vec3 worldNormal = normalize(vec3(world * vec4(_normal, 0.0)));

	// ground
	float luminance = prand_at(_normal, base, resolution, seed);
	luminance = clamp(luminance < lowerClip.x ? luminance * lowerClip.y : luminance, 0.0, 1.0);
	vec3 color = directNoise ? vec3(luminance) : to_color(luminance, range, lowerColor, upperColor, lowerClamp);
	color *= groundAlbedo;

	// diffuse
	float ndl = min(asin(max(0.0, dot(worldNormal, normalize(light - worldPosition))) + 0.05), 1.0);

	// Fresnel
	float fresnel = clamp(pow(0.65 + abs(dot(normalize(camera - worldPosition), worldNormal)), 16.0), 0.0, 1.0);

	// Emissive
	vec3 emissive = halo * (1.0 - fresnel) * clamp(1.0 - ndl, 0.2, 1.0);

	// Clouds
	if (clouds_enabled)
	{
		vec3 cloud = vec3(prand_at(vec3(rotation * vec4(_normal, 0.0)), clouds_base, resolution, seed) * clouds_albedo);
		float value = dot(cloud, cloud_magic);
		if (value >= 0.5) {
			color += cloud * (value - 0.5) / 0.5;
		}
	}

	gl_FragColor = vec4(color * ndl * fresnel + emissive, fresnel);
}
