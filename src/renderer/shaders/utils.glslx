const float PI = 3.14159265358979;

const vec4 prand_magic = vec4(12.9898, 78.233, 42.798, 89.5139);
const float prand_magic_2 = 43758.5453;

export float prand(float seed) {
	return fract(sin(seed * prand_magic.x) * prand_magic_2);
}

export float prand(vec2 seed) {
	return fract(sin(dot(seed, prand_magic.xy)) * prand_magic_2);
}

export float prand(vec3 seed) {
	return fract(sin(dot(seed, prand_magic.xyz)) * prand_magic_2);
}

export float prand(vec4 seed) {
	return fract(sin(dot(seed, prand_magic)) * prand_magic_2);
}

export vec4 prand(mat4 seed) {
	return vec4(prand(seed[0]), prand(seed[1]), prand(seed[2]), prand(seed[3]));
}

float prand_cos_mix(vec3 point, float seed) {
	vec3 f = floor(point);
	vec3 g = f + 1.0;
	vec3 a = (1.0 - cos(fract(point) * PI)) * 0.5;

	vec4 x = prand(mat4(
			f.x, f.y, f.z, seed,
			f.x, g.y, f.z, seed,
			f.x, f.y, g.z, seed,
			f.x, g.y, g.z, seed
		));

	vec4 y = prand(mat4(
			g.x, f.y, f.z, seed,
			g.x, g.y, f.z, seed,
			g.x, f.y, g.z, seed,
			g.x, g.y, g.z, seed
		));

	vec4 c4 = mix(x, y, a.x);
	vec2 c2 = mix(c4.xz, c4.yw, a.y);
	return mix(c2.x, c2.y, a.z);
}

float prand_at_part(vec3 point, float resolution, float seed) {
	return prand_cos_mix((point + 1.0) / 2.0 * resolution, seed) * 2.0 - 1.0;
}

export float prand_at(vec3 point, float base, float resolution, float seed) {
	float factor = 1.0
		+ prand_at_part(point, resolution / 32.0, seed) * 0.8
		+ prand_at_part(point, resolution / 16.0, seed) * 0.4
		+ prand_at_part(point, resolution / 8.0, seed) * 0.2
		+ prand_at_part(point, resolution / 4.0, seed) * 0.1
		+ prand_at_part(point, resolution / 2.0, seed) * 0.05
		+ prand_at_part(point, resolution, seed) * 0.025;

	return clamp(factor * base, 0.0, 1.0);
}

export vec3 to_color(float value, int num_colors, float range[5], vec3 colors[5], vec2 lower_clamp) {
	// Handle values below the range
	if (value < range[0]) {
		return colors[0] * clamp(value / range[0], lower_clamp.x, lower_clamp.y);
	}

	// Find the segment where the value falls
	for (int i = 1; i < num_colors; i++) {
		if (value >= range[i - 1] && value < range[i]) {
			// Calculate the blend
			float t = (value - range[i - 1]) / (range[i] - range[i - 1]);
			return mix(colors[i - 1], colors[i], t);
		}
	}

	// Handle values above the range
	return colors[num_colors - 1] * (value - range[num_colors - 1]) / (1.0 - range[num_colors - 1]);
}

export vec3 to_color(float value, float range[5], vec3 colors[5], vec2 lower_clamp) {
	if (value < range[0]) {
		return colors[0] * clamp(value / range[0], lower_clamp.x, lower_clamp.y);
	}

	vec3 upper = colors[1] * (value - range[0]) / (1.0 - range[0]);

	if (value >= range[1]) {
		return upper;
	}

	float scale_left = (value - range[0]) / (range[1] - range[0]);
	return colors[1] * (1.0 - scale_left) + upper;
}
