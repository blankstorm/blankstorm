const float PI = 3.14159265358979;

const vec4 randomMagic = vec4(12.9898, 78.233, 42.798, 89.5139);
const float randomMagicA = 43758.5453;

export float prand(float seed) {
	return fract(sin(seed * randomMagic.x) * randomMagicA);
}

export float prand(vec2 seed) {
	return fract(sin(dot(seed, randomMagic.xy)) * randomMagicA);
}

export float prand(vec3 seed) {
	return fract(sin(dot(seed, randomMagic.xyz)) * randomMagicA);
}

export float prand(vec4 seed) {
	return fract(sin(dot(seed, randomMagic)) * randomMagicA);
}

export vec4 prand(mat4 seed) {
	return vec4(prand(seed[0]), prand(seed[1]), prand(seed[2]), prand(seed[3]));
}

float prand_cos_mix(vec3 point, float seed) {
	vec3 f = floor(point);
	vec3 g = f + 1.0;
	vec3 a = (1.0 - cos(fract(point) * PI)) * 0.5;

	vec4 x = prand(mat4(
			f.x, f.y, f.z, seed,
			f.x, g.y, f.z, seed,
			f.x, f.y, g.z, seed,
			f.x, g.y, g.z, seed
		));

	vec4 y = prand(mat4(
			g.x, f.y, f.z, seed,
			g.x, g.y, f.z, seed,
			g.x, f.y, g.z, seed,
			g.x, g.y, g.z, seed
		));

	vec4 c4 = mix(x, y, a.x);
	vec2 c2 = mix(c4.xz, c4.yw, a.y);
	return mix(c2.x, c2.y, a.z);
}

export vec3 spherical(int face, vec2 uv) {
	uv -= 0.5;

	vec3 p = vec3(0.0);

	if (face == 0) { p = vec3(0.5, -uv.y, -uv.x); }
	if (face == 1) { p = vec3(-0.5, -uv.y, uv.x); }
	if (face == 2) { p = vec3(uv.x, 0.5, uv.y); }
	if (face == 3) { p = vec3(uv.x, -0.5, -uv.y); }
	if (face == 4) { p = vec3(uv.x, -uv.y, 0.5); }
	if (face == 5) { p = vec3(-uv.x, -uv.y, -0.5); }

	return normalize(p);
}

float luminance_factor(vec3 point, float resolution, float seed) {
	return prand_cos_mix((point + 1.0) / 2.0 * resolution, seed) * 2.0 - 1.0;
}

export float luminance_at(vec3 point, float base, float resolution, float seed) {
	float factor = 1.0
		+ luminance_factor(point, resolution / 32.0, seed) * 0.8
		+ luminance_factor(point, resolution / 16.0, seed) * 0.4
		+ luminance_factor(point, resolution / 8.0, seed) * 0.2
		+ luminance_factor(point, resolution / 4.0, seed) * 0.1
		+ luminance_factor(point, resolution / 2.0, seed) * 0.05
		+ luminance_factor(point, resolution, seed) * 0.025;

	return clamp(factor * base, 0.0, 1.0);
}

export vec3 luminance_to_color(float luminance, vec2 range, vec3 lowerColor, vec3 upperColor, vec2 lower_clamp) {
	if (luminance < range.x)
	{
		float scale = luminance / range.x;
		return lowerColor * clamp(scale, lower_clamp.x, lower_clamp.y);
	}
	else if (luminance < range.y)
	{
		float scale_left = (luminance - range.x) / (range.y - range.x);
		float scale_right = (luminance - range.x) / (1.0 - range.x);
		return lowerColor * (1.0 - scale_left) + upperColor * scale_right;
	}
	else
	{
		float scale = (luminance - range.x) / (1.0 - range.x);
		return upperColor * scale;
	}
}
